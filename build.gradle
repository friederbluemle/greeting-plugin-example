plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'maven-publish' // optional, only required for POM customization and publishing to custom repositories
}

group = 'org.samples.greeting'
version = '0.1'

gradlePlugin {
    plugins {
        greetingsPlugin {
            id = 'org.samples.greeting'
            implementationClass = 'org.example.greeting.GreetingPlugin'
        }
    }
}

pluginBundle {
    website = 'http://www.gradle.org/' // please change this URL to your own website/repository
    vcsUrl = 'https://github.com/gradle-guides/greeting-plugin-example'
    tags = ['example', 'template']

    plugins {
        greetingsPlugin {
            displayName = 'Greeting Plugin'
            description = 'Template for people to start their own plugin adventure'
        }
    }
}

publishing {
    // optional, only required for publishing to a custom repository
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
    // optional, only required for POM customization
    afterEvaluate {
        publications.getByName('pluginMaven') {
            pom {
                name = 'Gradle Guides Example Plugins'
                description = 'Template for people to start their own plugin adventure'
            }
        }
    }
}
